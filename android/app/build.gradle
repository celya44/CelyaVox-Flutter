plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dev.flutter.flutter-gradle-plugin'
}

def hasReleaseSigning = false

android {
    namespace "com.example.voip"
    compileSdk 35
    ndkVersion flutter.ndkVersion

    defaultConfig {
        applicationId "com.example.voip"
        minSdk 21
        targetSdk 35
        versionCode flutter.versionCode
        versionName flutter.versionName
    }

    signingConfigs {
        release {
            def ksPath = System.getenv("ANDROID_KEYSTORE_PATH") ?: "${project.rootDir}/app/upload-keystore.jks"
            def ksFile = file(ksPath)
            def ksPass = System.getenv("ANDROID_KEYSTORE_PASSWORD")
            def keyAliasEnv = System.getenv("ANDROID_KEY_ALIAS")
            def keyPass = System.getenv("ANDROID_KEY_PASSWORD")
            if (ksFile.exists() && ksPass && keyAliasEnv && keyPass) {
                storeFile ksFile
                storePassword ksPass
                keyAlias keyAliasEnv
                keyPassword keyPass
                enableV1Signing true
                enableV2Signing true
                hasReleaseSigning = true
            } else {
                println "WARN: Release signing not configured (missing keystore or env vars)."
            }
        }
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (hasReleaseSigning) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable true
            shrinkResources false
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.20"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.security:security-crypto:1.1.0-alpha06"
    implementation platform("com.google.firebase:firebase-bom:33.2.0")
    implementation "com.google.firebase:firebase-messaging"
}

// Ensure Flutter can find the generated APKs at project-level build/app/outputs/flutter-apk
def flutterApkDir = "${rootDir}/../build/app/outputs/flutter-apk"

tasks.register("copyDebugApkForFlutter", Copy) {
    dependsOn("packageDebug")
    from(fileTree("${buildDir}/outputs/apk/debug") { include("*.apk") })
    into(flutterApkDir)
}

tasks.matching { it.name == "assembleDebug" }.all {
    finalizedBy("copyDebugApkForFlutter")
}

tasks.register("copyReleaseApkForFlutter", Copy) {
    dependsOn("packageRelease")
    from(fileTree("${buildDir}/outputs/apk/release") { include("*.apk") })
    into(flutterApkDir)
    rename { fileName ->
        if (fileName.contains("-unsigned")) {
            "app-release.apk"
        } else {
            fileName
        }
    }
}

tasks.matching { it.name == "assembleRelease" }.all {
    finalizedBy("copyReleaseApkForFlutter")
}
