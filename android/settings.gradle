pluginManagement {
    def flutterSdkPath = System.getenv("FLUTTER_ROOT")
    if (flutterSdkPath == null) {
        def localPropertiesFile = new File(rootDir, "local.properties")
        if (localPropertiesFile.exists()) {
            def properties = new Properties()
            localPropertiesFile.withReader("UTF-8") { reader -> properties.load(reader) }
            flutterSdkPath = properties.getProperty("flutter.sdk")
        }
    }
    if (flutterSdkPath == null) {
        throw new GradleException("Flutter SDK not found. Set FLUTTER_ROOT or define flutter.sdk in local.properties")
    }
    includeBuild("${flutterSdkPath}/packages/flutter_tools/gradle")
    repositories {
        maven {
            url "https://maven-central.storage-download.googleapis.com/maven2"
            content { includeGroupByRegex "org\\.jetbrains\\.kotlin.*" }
        }
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://plugins.gradle.org/m2/" }
    }
}
plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.6.0" apply false
    id "org.jetbrains.kotlin.android" version "1.9.23" apply false
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)
    repositories {
        maven {
            url "https://maven-central.storage-download.googleapis.com/maven2"
            content { includeGroupByRegex "org\\.jetbrains\\.kotlin.*" }
        }
        google()
        mavenCentral()
    }
}

include ":app"
