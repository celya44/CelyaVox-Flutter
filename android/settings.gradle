pluginManagement {
    def flutterSdkPath = System.getenv("FLUTTER_ROOT")
    if (flutterSdkPath == null) {
        def localPropertiesFile = new File(rootDir, "local.properties")
        if (localPropertiesFile.exists()) {
            def properties = new Properties()
            localPropertiesFile.withReader("UTF-8") { reader -> properties.load(reader) }
            flutterSdkPath = properties.getProperty("flutter.sdk")
        }
    }
    if (flutterSdkPath == null) {
        throw new GradleException("Flutter SDK not found. Set FLUTTER_ROOT or define flutter.sdk in local.properties")
    }
    gradle.ext.flutterSdkPath = flutterSdkPath

    includeBuild("${flutterSdkPath}/packages/flutter_tools/gradle")
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://maven-central.storage-download.googleapis.com/maven2" }
    }

    resolutionStrategy {
        eachPlugin {
            if (requested.id.id.startsWith("org.jetbrains.kotlin")) {
                useVersion("1.9.20")
            }
        }
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.6.0" apply false
    id "org.jetbrains.kotlin.android" version "1.9.20" apply false
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)
    repositories {
        def flutterSdkPath = gradle.ext.flutterSdkPath
        if (flutterSdkPath != null) {
            maven {
                url = uri("${flutterSdkPath}/bin/cache/artifacts/engine/android")
            }
        }
        maven {
            url "https://storage.googleapis.com/download.flutter.io"
        }
        maven {
            url "https://maven-central.storage-download.googleapis.com/maven2"
            content { includeGroupByRegex "org\\.jetbrains\\.kotlin.*" }
        }
        google()
        mavenCentral()
    }
}

include ":app"
